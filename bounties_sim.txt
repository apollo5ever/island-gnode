Function Initialize() Uint64
10 STORE("CEO",HEX(SCID()))
20 SEND_ASSET_TO_ADDRESS(SIGNER(),1000000000,SCID())
30 STORE("QUORUM",0)
40 STORE("EPOCH-INIT",1691607914)
70 STORE("treasuryDERO",0)
99 RETURN 0
End Function

Function BT(H String, i Uint64, J String, X String, E Uint64, name String) Uint64
15 IF EXISTS(H + i + "_T") THEN GOTO 40
16 IF DEROVALUE() < 100000 THEN GOTO 100
17 IF ASSETVALUE(HEXDECODE(H)) != 1 THEN GOTO 100
18 STORE("T_DERO",LOAD("T_DERO")+DEROVALUE()*5/100)
20 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(H))
21 STORE(H+i+"_X0",X)
22 STORE(H+i+"_XN",0)
23 STORE(H+i+"_XT",1)
24 STORE(H+i+"_XE",BLOCK_TIMESTAMP()+1209600)
25 STORE(H+i+"_T",DEROVALUE()*95/100)
26 STORE(H+i+"_J0",J)
27 STORE(H+i+"_E",E)
28 STORE(H+i+"_name_0",name)
29 STORE(ADDRESS_STRING(SIGNER())+"_"+H+i+"_S",DEROVALUE()*95/100)
30 STORE(H+i+"_JN",0)
31 STORE(H+i+"_JE",BLOCK_TIMESTAMP()+1209600)
32 STORE(H+i+"_JT",1)
33 STORE(H+i+"_version",0)
34 RETURN 0
40 STORE(H+i+"_T",(DEROVALUE()*95/100)+LOAD(H+i+"_T"))
41 STORE("T_DERO",LOAD("T_DERO")+DEROVALUE()*5/100)
42 IF EXISTS(ADDRESS_STRING(SIGNER())+"_"+H+i+"_S") THEN GOTO 45
43 STORE(ADDRESS_STRING(SIGNER())+"_"+H+i+"_S",DEROVALUE()*95/100)
44 RETURN 0
45 STORE(ADDRESS_STRING(SIGNER())+"_"+H+i+"_S",(DEROVALUE()*95/100)+LOAD(ADDRESS_STRING(SIGNER())+"_"+H+i+"_S"))
99 RETURN 0
100 RETURN 1
End Function

Function SetName(H String,i Uint64, Name String) Uint64
10 IF ASSETVALUE(HEXDECODE(H)) != 1 THEN GOTO 100
20 dim version as Uint64
30 let version = LOAD(H+i+"_version") + 1
40 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(H))
50 STORE(H+i+"_name_"+version,Name)
60 STORE(H+i+"_version",version)
99 RETURN 0
100 RETURN 1
End Function

Function SetImage(H String,i Uint64, Image String) Uint64
10 IF ASSETVALUE(HEXDECODE(H)) != 1 THEN GOTO 100
20 dim version as Uint64
30 let version = LOAD(H+i+"_version") + 1
40 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(H))
50 STORE(H+i+"_image_"+version,Image)
60 STORE(H+i+"_version",version)
99 RETURN 0
100 RETURN 1
End Function

Function SetTagline(H String, i Uint64, Tagline String) Uint64
10 IF ASSETVALUE(HEXDECODE(H)) != 1 THEN GOTO 100
20 dim version as Uint64
30 let version = LOAD(H+i+"_version") + 1
40 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(H))
50 STORE(H+i+"_tagline_"+version,Tagline)
60 STORE(H+i+"_version",version)
99 RETURN 0
100 RETURN 1
End Function

Function SetDescription(H String, i Uint64, Description String) Uint64
10 IF ASSETVALUE(HEXDECODE(H)) != 1 THEN GOTO 100
20 dim version as Uint64
30 let version = LOAD(H+i+"_version") + 1
40 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(H))
50 STORE(H+i+"_desc_"+version,Description)
60 STORE(H+i+"_version",version)
99 RETURN 0
100 RETURN 1
End Function

Function SetMetadata(H String, i Uint64, Name String, Image String, Tagline String, Description String) Uint64
10 IF ASSETVALUE(HEXDECODE(H)) != 1 THEN GOTO 100
20 dim version as Uint64
30 let version = LOAD(H+i+"_version") + 1
40 STORE(H+i+"_image_"+version,Image)
50 STORE(H+i+"_tagline_"+version,Tagline)
60 STORE(H+i+"_desc_"+version,Description)
70 STORE(H+i+"_name_"+version,Name)
80 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(H))
90 STORE(H+i+"_version",version)
99 RETURN 0
100 RETURN 1
End Function

Function AN(H String, JX String, l String) Uint64
10 IF ASSETVALUE(HEXDECODE(JX)) != 1 THEN GOTO 100
15 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(JX))
20 IF BLOCK_TIMESTAMP() > LOAD(H+"_"+l+"E") THEN GOTO 70
30 IF LOAD(H+"_"+l+LOAD(H+"_"+l+"N")) != JX THEN GOTO 100
40 STORE(H+"_"+l,JX)
50 STORE(H+"_"+l+"E",BLOCK_TIMESTAMP()+1209600)
55 STORE(H+"_"+l+"_address",ADDRESS_STRING(SIGNER()))
60 RETURN 0
70 dim k as Uint64
71 LET k = 1+(BLOCK_TIMESTAMP() - LOAD(H+"_"+l+"E"))/1209600
75 IF LOAD(H+"_"+l+((LOAD(H+"_"+l+"N")+k)%LOAD(H+"_"+l+"T"))) != JX THEN GOTO 100
80 STORE(H+"_"+l,JX)
85 STORE(H+"_"+l+"_address",ADDRESS_STRING(SIGNER()))
90 STORE(H+"_"+l+"E",BLOCK_TIMESTAMP()+1209600)
95 STORE(H+"_"+l+"N",(LOAD(H+"_"+l+"N")+k)%LOAD(H+"_"+l+"T"))
99 RETURN 0
100 RETURN 1
End Function

Function N(H String, i Uint64, JX String, l String) Uint64
10 IF ASSETVALUE(HEXDECODE(H)) != 1 THEN GOTO 100
15 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(H))
20 STORE(H+i+"_"+l+LOAD(H+i+"_"+l+"T"),JX)
30 STORE(H+i+"_"+l+"T",LOAD(H+i+"_"+l+"T")+1)
40 IF LOAD(H+i+"_"+l+"T") != 2 THEN GOTO 99
50 IF BLOCK_TIMESTAMP()<LOAD(H+i+"_"+l+"E") THEN GOTO 99
60 STORE(H+i+"_"+l+"E",BLOCK_TIMESTAMP()+604800)
99 RETURN 0
100 RETURN 1
End Function

Function ATR(H String, R String, I Uint64, W Uint64, F Uint64) Uint64
10 IF ASSETVALUE(HEXDECODE(LOAD(H+"_J"))) != 1 THEN GOTO 100
12 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(LOAD(H+"_J")))
15 IF EXISTS(H+"_JF") THEN GOTO 100
20 IF EXISTS(H+"_RN") THEN GOTO 30
25 STORE(H+"_RN",0)
30 IF EXISTS(H+"_R_"+I) THEN GOTO 40
35 STORE(H+"_RN",LOAD(H+"_RN")+1)
40 STORE(H+"_R_"+I,R)
50 STORE(H+"_W_"+I,W)
60 IF F == 0 THEN GOTO 99
70 STORE(H+"_JF",1)
99 RETURN 0
100 RETURN 1
End Function

Function V(H String) Uint64
10 IF ASSETVALUE(HEXDECODE(LOAD(H+"_X"))) != 1 THEN GOTO 100
15 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(LOAD(H+"_X")))
20 DELETE(H+"_JF")
99 RETURN 0
100 RETURN 1
End Function

Function RT(H String) Uint64
10 IF LOAD(H+"_E") <BLOCK_TIMESTAMP() THEN GOTO 95
15 IF EXISTS(H+"_JF")==0 THEN GOTO 100
16 IF ASSETVALUE(HEXDECODE(LOAD(H+"_X"))) != 1 THEN GOTO 100
17 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(LOAD(H+"_X")))
21 IF LOAD(H+"_JF") == 2 THEN GOTO 100
22 STORE(H+"_JF",2) // NEED NEW WAY TO PAY JUDGE/EXECUTOR SHOULD BE PARAM DECIDED AT THE OUTSET
30 dim i,rn,total as Uint64
35 LET total = 0
40 LET i = LOAD(H+"_RN") 
45 LET rn = LOAD(H+"_RN")
50 LET i = i-1
60 LET total = total + LOAD(H+"_W_"+i)
65 IF i == 0 THEN GOTO 70 ELSE GOTO 50
70 SEND_DERO_TO_ADDRESS(ADDRESS_RAW(LOAD(H+"_R_"+i)),LOAD(H+"_T")*LOAD(H+"_W_"+i)*90/(total*100))
80 LET i = i+1
90 IF i == rn THEN GOTO 99 ELSE GOTO 70
95 IF EXISTS(ADDRESS_STRING(SIGNER())+"_"+H+"_S")==0 THEN GOTO 100
96 SEND_DERO_TO_ADDRESS(SIGNER(),LOAD(ADDRESS_STRING(SIGNER())+"_"+H+"_S"))
97 DELETE(ADDRESS_STRING(SIGNER())+"_"+H+"_S")
99 RETURN 0
100 RETURN 1
End Function

Function D() Uint64
10 IF ASSETVALUE(HEXDECODE(LOAD("COCO"))) % 10000 != 0 THEN GOTO 100
13 STORE("T_DERO",LOAD("T_DERO")+DEROVALUE())
14 STORE("T_COCO",LOAD("T_COCO")+ASSETVALUE(HEXDECODE(LOAD("COCO"))))
15 STORE(ADDRESS_STRING(SIGNER())+"_EPOCH",(BLOCK_TIMESTAMP() - LOAD("EPOCH-INIT"))/2629743)
16 IF EXISTS(ADDRESS_STRING(SIGNER())+"_SHARES") THEN GOTO 30
20 STORE(ADDRESS_STRING(SIGNER())+"_SHARES",ASSETVALUE(HEXDECODE(LOAD("COCO")))/10000)
25 RETURN 0
30 STORE(ADDRESS_STRING(SIGNER())+"_SHARES",LOAD(ADDRESS_STRING(SIGNER())+"_SHARES")+ASSETVALUE(HEXDECODE(LOAD("COCO")))/10000)
35 RETURN 0
100 RETURN 1
End Function

Function W() Uint64
10 DIM EPOCH as Uint64
11 LET EPOCH = (BLOCK_TIMESTAMP()-LOAD("EPOCH-INIT"))/2629743
14 IF EXISTS(ADDRESS_STRING(SIGNER())+"_SHARES") ==0 THEN GOTO 100
15 IF LOAD(ADDRESS_STRING(SIGNER())+"_EPOCH") >= EPOCH THEN GOTO 100
16 DIM SHARE as Uint64
17 LET SHARE = LOAD(ADDRESS_STRING(SIGNER())+"_SHARES")*10000*LOAD("T_DERO")/(LOAD("T_COCO"))
18 SEND_DERO_TO_ADDRESS(SIGNER(),SHARE)
19 STORE(ADDRESS_STRING(SIGNER())+"_EPOCH",EPOCH)
20 STORE("T_DERO",LOAD("T_DERO")-SHARE)
25 RETURN 0
100 RETURN 1
End Function

Function SS(shares Uint64) Uint64
10 IF EXISTS(ADDRESS_STRING(SIGNER())+"_SHARES") == 0 THEN GOTO 100
20 IF LOAD(ADDRESS_STRING(SIGNER())+"_SHARES") < shares THEN GOTO 100
30 STORE(ADDRESS_STRING(SIGNER())+"_SHARES",LOAD(ADDRESS_STRING(SIGNER())+"_SHARES")-shares)
40 SEND_ASSET_TO_ADDRESS(SIGNER(),shares*10000,HEXDECODE(LOAD("COCO")))
50 STORE("T_COCO",LOAD("T_COCO")-shares*10000)
99 RETURN 0
100 RETURN 1
End Function

Function Propose(hash String, k String, u Uint64, s String, t Uint64, seat Uint64) Uint64
10 IF ASSETVALUE(HEXDECODE(LOAD("CEO"))) != 1 THEN GOTO 13
11 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(LOAD("CEO")))
12 GOTO 15
13 IF ASSETVALUE(HEXDECODE(LOAD("SEAT_"+seat))) !=1 THEN GOTO 100
14 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(LOAD("SEAT_"+seat)))
15 STORE("APPROVE", 0)
20 IF hash =="" THEN GOTO 40
25 STORE("HASH",hash)
30 STORE("k","")
35 RETURN 0
40 STORE("k",k)
45 STORE("HASH","")
49 STORE("t",t)
50 IF t == 1 THEN GOTO 80
60 STORE("s", s)
70 RETURN 0
80 STORE("u",u)
90 RETURN 0
100 RETURN 1
End Function

Function Approve(seat Uint64) Uint64
10 IF ASSETVALUE(HEXDECODE(LOAD("SEAT_"+seat)))!=1 THEN GOTO 100
20 STORE("APPROVE",LOAD("APPROVE")+1)
30 STORE("SEAT_"+seat+"_OWNER",SIGNER())
99 RETURN 0
100 RETURN 1
End Function

Function ClaimSeat(seat Uint64) Uint64
10 IF SIGNER()!= LOAD("SEAT_"+seat+"_OWNER") THEN GOTO 100
20 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(LOAD("SEAT_"+seat)))
30 IF LOAD("APPROVE") == 0 THEN GOTO 99
40 STORE("APPROVE",LOAD("APPROVE")-1)
99 RETURN 0
100 RETURN 1
End Function

Function Update(code String) Uint64
10 IF ASSETVALUE(HEXDECODE(LOAD("CEO")))!=1 THEN GOTO 100
15 SEND_ASSET_TO_ADDRESS(SIGNER(),1,HEXDECODE(LOAD("CEO")))
20 IF SHA256(code) != HEXDECODE(LOAD("HASH")) THEN GOTO 100
30 IF LOAD("APPROVE") < LOAD("QUORUM") THEN GOTO 100
40 UPDATE_SC_CODE(code)
99 RETURN 0
100 RETURN 1
End Function

Function Store(k String, u Uint64, s String) Uint64
20 IF k != LOAD("k") THEN GOTO 999
40 IF LOAD("APPROVE") < LOAD("QUORUM") THEN GOTO 999
45 STORE("APPROVE",0)
50 dim t as Uint64
60 let t = LOAD("t")
110 IF t == 0 THEN GOTO 150
120 IF t == 1 THEN GOTO 170
130 IF s!=LOAD("s") THEN GOTO 999
135 STORE(k, HEX(s))
140 RETURN 0
150 IF s!=LOAD("s") THEN GOTO 999
155 STORE(k, s)
160 RETURN 0
170 IF u!=LOAD("u") THEN GOTO 999
175 STORE(k,u)
180 RETURN 0
999 RETURN 1
End Function